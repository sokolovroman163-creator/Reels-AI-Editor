import QtQuick
import QtQuick.Controls.Basic
import QtQuick.Shapes
import Drift
import ".."

// Touch transform overlay: move/scale/rotate the clips visible at the playhead.
// (The in-place text editor is still wired up but no longer reachable: the
// properties panel owns text editing until the editor matches the render.) Same maths as the desktop TransformOverlay, but
// the grips carry a fingertip-sized hit area instead of a mouse-sized one, and
// the mouse-only affordances (hover, wheel pass-through, arrow-key nudging,
// Shift/Ctrl modifiers) are gone — a phone has none of them. Corner grips
// therefore keep footage aspect unconditionally and snapping is always on.
// Geometry (x/y/width/height/z/visible) is driven by AndroidPreview.
Item {
    id: root

    property var overlayClips: []
    // True while a handle is being dragged. Rebuilding the model mid-drag would
    // destroy the delegate that owns the active grab, so refreshes are
    // suppressed until the drag ends. (Also held true while a text clip is
    // edited in place, so the inline editor delegate is not destroyed mid-edit.)
    property bool interacting: false

    // The selected clip's gizmo, when it is a 3D layer, and its pose while a handle is dragged.
    readonly property var gizmoBox: {
        for (const b of overlayClips) {
            if (b.layer3d && b.kind !== "model3d" && b.track === EditorState.selectedTrack
                    && b.clip === EditorState.selectedClip)
                return b
        }
        return null
    }
    readonly property var gizmoLive: gizmo.livePose

    // A transform layer being moved: the clips under it are drawn from a model that is not
    // rebuilt mid-drag, so their outlines would hang in the old place and are hidden meanwhile.
    property int movingLayerTrack: -1
    readonly property int hiddenChildrenOf: movingLayerTrack >= 0 ? movingLayerTrack
        : (gizmoLive && gizmoBox && gizmoBox.kind === "transform" ? gizmoBox.track : -1)

    readonly property var selectedBox: {
        for (const b of overlayClips) {
            if (b.track === EditorState.selectedTrack && b.clip === EditorState.selectedClip)
                return b
        }
        return null
    }
    // The transform clip over the selected clip (innermost), for its dashed frame and chip.
    readonly property var parentFrameBox: {
        const sel = selectedBox
        if (!sel || sel.kind === "transform" || !sel.parents || sel.parents.length === 0)
            return null
        const p = sel.parents[0]
        for (const b of overlayClips) {
            if (b.kind === "transform" && b.track === p.track && b.clip === p.clip)
                return b
        }
        return null
    }

    // "track:clip" of the text clip currently edited in place, or "".
    property string editingKey: ""

    // "track:clip" of a freshly added placeholder text clip that should open its
    // editor as soon as its box exists.
    property string pendingEditKey: ""

    // Visible dot; the touch target around it is gripTouch wide.
    readonly property real gripSize: 14
    readonly property real gripTouch: 44

    // True when two overlay models describe the same boxes. Text/name updates
    // still reach delegates via liveStyle, so rebuilding for every
    // keystroke is unnecessary.
    function clipsOverlayEqual(a, b) {
        if (!a || !b || a.length !== b.length)
            return false
        for (let i = 0; i < a.length; ++i) {
            const x = a[i]
            const y = b[i]
            if (x.track !== y.track || x.clip !== y.clip || x.kind !== y.kind
                    || x.x !== y.x || x.y !== y.y
                    || x.width !== y.width || x.height !== y.height
                    || x.rotation !== y.rotation
                    || x.layer3d !== y.layer3d
                    || x.rotationX !== y.rotationX || x.rotationY !== y.rotationY
                    || x.z !== y.z || x.perspective !== y.perspective
                    || x.canvasWidth !== y.canvasWidth
                    || x.canvasHeight !== y.canvasHeight
                    || x.parentSig !== y.parentSig)
                return false
        }
        return true
    }

    function sameOverlayClips(a, b) {
        if (!a || !b || a.length !== b.length)
            return false
        for (let i = 0; i < a.length; ++i) {
            if (a[i].track !== b[i].track || a[i].clip !== b[i].clip || a[i].kind !== b[i].kind)
                return false
        }
        return true
    }

    function refreshOverlay() {
        if (interacting)
            return
        if (EditorState.playing)
            return
        const next = EditorState.previewClipsAtPlayhead()
        if (clipsOverlayEqual(overlayClips, next))
            return
        const sameClips = sameOverlayClips(overlayClips, next)
        overlayClips = next
        // Only geometry moved (a slider or another handle dragging this clip): the delegates
        // follow through `box`, and recreating them would drop a selected handle's focus.
        if (sameClips)
            return
        // Set model imperatively. Binding `model: overlayClips` re-enters when
        // tracksChanged fires during delegate setup (binding loop on model).
        clipRepeater.model = next
    }

    function endInteraction() {
        EditorState.commitPreviewDrag()
        interacting = false
        movingLayerTrack = -1
        snapGuideX = -1
        snapGuideY = -1
        Qt.callLater(refreshOverlay)
    }

    // Stickiness: while a clip is moved or resized its edges and centre pull to
    // the canvas edges, centre lines and visible guides. The tolerance is a screen distance, so
    // the pull feels the same whatever the project resolution or preview zoom.
    readonly property real snapTolPx: 10

    // Engaged guide lines, in overlay px, or -1 when nothing is snapped.
    property real snapGuideX: -1
    property real snapGuideY: -1

    onSnapGuideXChanged: if (snapGuideX >= 0) Haptics.detent()
    onSnapGuideYChanged: if (snapGuideY >= 0) Haptics.detent()

    // Nearest target within `tol` of any candidate, returned as the delta to add
    // to the moving value. `guide` is the target that won, or -1 for no snap.
    function snapAxis(candidates, targets, tol) {
        let result = { delta: 0, guide: -1 }
        let best = tol
        for (const c of candidates) {
            for (const t of targets) {
                const d = t - c
                if (Math.abs(d) < best) {
                    best = Math.abs(d)
                    result = { delta: d, guide: t }
                }
            }
        }
        return result
    }

    Component.onCompleted: refreshOverlay()

    Connections {
        target: EditorState
        function onTracksChanged() { root.refreshOverlay() }
        function onClipPropertiesPreviewed(trackIndex, clipIndex, keys) {
            for (const k of keys) {
                if (k === "x" || k === "y" || k === "width" || k === "height" || k === "rotation"
                        || k === "rotationX" || k === "rotationY" || k === "z" || k === "perspective") {
                    root.refreshOverlay()
                    return
                }
            }
        }
        function onSelectionChanged() { root.refreshOverlay() }
        // A scrub moves the playhead per scroll event; catch up once when it ends.
        function onPlayheadSecondsChanged() {
            if (!EditorState.scrubbing)
                root.refreshOverlay()
        }
        function onScrubbingChanged() {
            if (!EditorState.scrubbing)
                root.refreshOverlay()
        }
        function onPlayingChanged() {
            if (!EditorState.playing)
                root.refreshOverlay()
        }
    }

    Repeater {
        id: clipRepeater

        delegate: Item {
            id: handle
            required property var modelData
            required property int index
            // Latest geometry; the model itself is only swapped when the set of clips changes.
            readonly property var box: root.overlayClips[index] || modelData

            readonly property bool selected: EditorState.selectedTrack === box.track
                                             && EditorState.selectedClip === box.clip
            // A transform layer's own box: only there while selected, so taps otherwise reach
            // the clips it moves.
            readonly property bool isTransform: box.kind === "transform"
            readonly property bool hasParent: box.parentActive === true
            readonly property var parentInverse: hasParent
                ? EditorState.previewParentOverlayMatrix(box, handle.sx).inverted() : null
            // Overlay point (in the parented frame) -> the box's own overlay frame.
            function toParentLocal(p) {
                if (!handle.hasParent)
                    return p
                const v = handle.parentInverse.times(Qt.vector4d(p.x, p.y, 0, 1))
                return Qt.point(v.x / v.w, v.y / v.w)
            }
            readonly property bool coveredBySelectedLayer: {
                for (const p of (box.parents || [])) {
                    if (p.track === EditorState.selectedTrack && p.clip === EditorState.selectedClip)
                        return true
                }
                return false
            }
            readonly property bool childOfMovingLayer: {
                if (root.hiddenChildrenOf < 0)
                    return false
                for (const p of (box.parents || [])) {
                    if (p.track === root.hiddenChildrenOf)
                        return true
                }
                return false
            }
            visible: (!isTransform || selected) && !childOfMovingLayer
            enabled: visible
            readonly property bool isText: box.kind === "text"
            // A 3D model: the box is the projected model, not the clip's layout rect, so a drag
            // moves the clip anchor by the box delta and there is nothing to resize or spin.
            readonly property bool isModel3d: box.kind === "model3d"
            // Tilted or pushed in depth: the box is drawn through the clip's perspective
            // transform, so its outline and grips land on the rendered quad.
            // The gizmo's live pose while it drags this clip: the model is not rebuilt mid-drag.
            readonly property var gizmoPose: root.gizmoLive && root.gizmoLive.track === box.track
                                             && root.gizmoLive.clip === box.clip ? root.gizmoLive : null
            readonly property var pose3d: gizmoPose || box
            readonly property bool is3d: !isModel3d && ((pose3d.rotationX || 0) !== 0
                                                         || (pose3d.rotationY || 0) !== 0
                                                         || (pose3d.z || 0) !== 0)
            // How much the perspective magnifies the clip's plane at its depth, so a body drag
            // keeps the clip under the finger.
            readonly property real depthScale: is3d
                ? (box.perspective || 2000) / Math.max(1, (box.perspective || 2000) - (pose3d.z || 0))
                : 1
            readonly property real anchorOffsetX: box.anchorX !== undefined ? box.anchorX - box.x : 0
            readonly property real anchorOffsetY: box.anchorY !== undefined ? box.anchorY - box.y : 0
            readonly property bool editing: root.editingKey
                                            === (box.track + ":" + box.clip)
            // True when this clip was just added with no text and should open
            // with an empty editor instead of the placeholder string.
            property bool openAsPlaceholder: false
            // Live clip style — tracks EditorState so property-sheet edits apply
            // to the inline editor in real time.
            readonly property var liveStyle: {
                void EditorState.tracksRevision
                const info = EditorState.clipAt(box.track, box.clip)
                return info && info.textStyle ? info.textStyle : null
            }
            readonly property var liveClip: {
                void EditorState.tracksRevision
                return EditorState.clipAt(box.track, box.clip)
            }

            function enterEdit() {
                root.editingKey = box.track + ":" + box.clip
                root.interacting = true
                EditorState.selectClip(box.track, box.clip)
                EditorState.beginTextEdit(box.track, box.clip)
                const info = EditorState.clipAt(box.track, box.clip)
                if (handle.openAsPlaceholder) {
                    editor.text = ""
                    EditorState.previewSetClipTextContent(box.track, box.clip, "")
                    handle.openAsPlaceholder = false
                } else {
                    editor.text = info.textContent || ""
                }
                editor.forceActiveFocus()
                editor.selectAll()
            }

            function commitEdit() {
                if (!handle.editing)
                    return
                EditorState.commitTextEdit(box.track, box.clip, editor.text)
                handle.finishEdit()
            }

            function cancelEdit() {
                if (!handle.editing)
                    return
                EditorState.cancelPreviewDrag()
                handle.finishEdit()
            }

            function finishEdit() {
                root.editingKey = ""
                EditorState.endTextEdit()
                root.interacting = false
                Qt.callLater(root.refreshOverlay)
            }

            // A just-added placeholder clip opens its own editor. Checked both on
            // creation and when the key arrives, since either can happen first.
            function claimPendingEdit() {
                if (!handle.isText || handle.editing)
                    return
                if (root.pendingEditKey !== (box.track + ":" + box.clip))
                    return
                root.pendingEditKey = ""
                handle.openAsPlaceholder = true
                Qt.callLater(handle.enterEdit)
            }

            Component.onCompleted: handle.claimPendingEdit()

            Connections {
                target: root
                function onPendingEditKeyChanged() { handle.claimPendingEdit() }
            }

            readonly property real canvasW: Math.max(1, box.canvasWidth)
            readonly property real canvasH: Math.max(1, box.canvasHeight)
            readonly property real sx: parent.width / canvasW
            readonly property real sy: parent.height / canvasH

            // Live overrides applied during a drag so the box tracks the finger
            // without rebuilding the (stale) model.
            property real liveX: -1e12
            property real liveY: -1e12
            property real liveW: -1
            property real liveH: -1
            property real liveRotation: 1e9

            readonly property real layoutX: gizmoPose ? gizmoPose.x : liveX > -1e11 ? liveX : box.x
            readonly property real layoutY: gizmoPose ? gizmoPose.y : liveY > -1e11 ? liveY : box.y
            readonly property real layoutW: gizmoPose ? gizmoPose.width : liveW >= 0 ? liveW : box.width
            readonly property real layoutH: gizmoPose ? gizmoPose.height : liveH >= 0 ? liveH : box.height
            readonly property real centerX: (layoutX + layoutW * 0.5) * sx
            readonly property real centerY: (layoutY + layoutH * 0.5) * sy

            x: layoutX * sx
            y: layoutY * sy
            width: Math.max(24, layoutW * sx)
            height: Math.max(24, layoutH * sy)
            // Front-most track (lowest index) sits on top so it wins tap
            // hit-testing over boxes behind it. The clip selected on the timeline
            // is raised above all of them, so a box that lies under a full-frame
            // clip on an upper track is still the one the tap reaches. The clip
            // being edited jumps above everything so its editor and the tap-away
            // catcher order correctly.
            z: handle.editing ? 1000 : handle.selected ? 900 : -box.track
            transformOrigin: Item.Center
            readonly property real layoutRotation: gizmoPose ? gizmoPose.rotation
                                                   : liveRotation < 1e8 ? liveRotation : box.rotation
            rotation: is3d || hasParent ? 0 : layoutRotation
            transform: Matrix4x4 {
                id: poseTransform
                matrix: handle.is3d || handle.hasParent
                        ? EditorState.previewClipPoseMatrix({
                                                                "canvasWidth": handle.box.canvasWidth,
                                                                "canvasHeight": handle.box.canvasHeight,
                                                                "rotationX": handle.pose3d.rotationX || 0,
                                                                "rotationY": handle.pose3d.rotationY || 0,
                                                                "z": handle.pose3d.z || 0,
                                                                "perspective": handle.box.perspective || 2000,
                                                                "parentActive": handle.box.parentActive,
                                                                "parent": handle.box.parent
                                                            }, handle.layoutX, handle.layoutY,
                                                            handle.layoutW, handle.layoutH,
                                                            handle.layoutRotation, handle.sx, handle.sy)
                        : Qt.matrix4x4()
            }

            // Overlay point -> this box's own (untilted) layout px, through the pose as it stands
            // now. Taken at a grab and reused for the whole drag, since the pose follows the drag.
            function overlayToLocalMapper() {
                const m = Qt.matrix4x4(1, 0, 0, handle.x, 0, 1, 0, handle.y, 0, 0, 1, 0, 0, 0, 0, 1)
                        .times(poseTransform.matrix).inverted()
                return function(px, py) {
                    const v = m.times(Qt.vector4d(px, py, 0, 1))
                    return Qt.point(v.x / v.w, v.y / v.w)
                }
            }

            property real dragStartX: 0
            property real dragStartY: 0
            property real dragStartW: 1
            property real dragStartH: 1
            property int dragStartPixelSize: 64
            // True while a resize grip is held, for the size readout.
            property bool resizing: false

            // Visible guides, in layout px. Reading guideItems re-runs this when the
            // active sets change.
            readonly property var guideSnap: EditorState.guidesEnabled && EditorState.guideItems.length > 0
                ? EditorState.guideSnapTargets(handle.canvasW, handle.canvasH) : ({ x: [], y: [] })
            // Canvas edges and centre lines, plus the guides.
            readonly property var snapTargetsX: [0, handle.canvasW / 2, handle.canvasW].concat(guideSnap.x)
            readonly property var snapTargetsY: [0, handle.canvasH / 2, handle.canvasH].concat(guideSnap.y)
            readonly property real snapTolX: root.snapTolPx / handle.sx
            readonly property real snapTolY: root.snapTolPx / handle.sy
            // A rotated box has no axis-aligned edges to stick with, so it does
            // not snap — pulling its bounding box would move it sideways.
            readonly property bool canSnap: !handle.is3d && !handle.hasParent
                                            && Math.abs(handle.layoutRotation) < 0.01

            // Guides are published in overlay px so they can be drawn once, at
            // root level, spanning the whole canvas rather than the clip box.
            function publishGuides(gx, gy) {
                root.snapGuideX = gx >= 0 ? gx * handle.sx : -1
                root.snapGuideY = gy >= 0 ? gy * handle.sy : -1
            }

            Connections {
                target: EditorState
                function onSelectedClipDataChanged() {
                    if (!handle.editing || editor.activeFocus)
                        return
                    const info = EditorState.clipAt(box.track, box.clip)
                    if (!info)
                        return
                    const next = info.textContent || ""
                    if (editor.text !== next)
                        editor.text = next
                }
            }

            Rectangle {
                anchors.fill: parent
                color: "transparent"
                border.width: (handle.selected || handle.editing)
                              ? Theme.borderWidthFocus : Theme.borderWidth
                border.color: handle.selected ? (handle.isTransform ? Theme.clipTransform : Theme.primary)
                              : handle.coveredBySelectedLayer ? Theme.clipTransform : Theme.guideStrong
                radius: Theme.radiusXs

                Behavior on border.width {
                    NumberAnimation { duration: Theme.durationFast; easing.type: Theme.easing }
                }
                Behavior on border.color {
                    ColorAnimation { duration: Theme.durationFast; easing.type: Theme.easing }
                }
            }

            // In-place text editor. Shown over the box while editing; the baked
            // raster is hidden by the compositor via beginTextEdit. Plain TextArea
            // (not a Themed* control): this is canvas content that must match the
            // rendered text, not app chrome.
            TextArea {
                id: editor
                anchors.fill: parent
                visible: handle.editing
                enabled: handle.editing
                background: null
                renderType: TextEdit.NativeRendering
                padding: handle.liveStyle && handle.liveStyle.boxEnabled
                         ? Math.max(0, handle.liveStyle.boxPadding * handle.sy) : 0
                color: handle.liveStyle ? handle.liveStyle.color : "white"
                font.family: handle.liveStyle ? handle.liveStyle.fontFamily : Theme.fontFamily
                font.pixelSize: handle.liveStyle
                                ? Math.max(1, Math.round(handle.liveStyle.pixelSize * handle.sy))
                                : 16
                font.weight: handle.liveStyle ? handle.liveStyle.fontWeight : Font.Normal
                font.italic: handle.liveStyle ? handle.liveStyle.italic : false
                font.letterSpacing: handle.liveStyle ? handle.liveStyle.letterSpacing * handle.sy : 0
                wrapMode: (handle.liveStyle && handle.liveStyle.wordWrap === false)
                          ? TextEdit.NoWrap : TextEdit.WordWrap
                horizontalAlignment: !handle.liveStyle ? TextEdit.AlignHCenter
                                     : handle.liveStyle.align === "left" ? TextEdit.AlignLeft
                                     : handle.liveStyle.align === "right" ? TextEdit.AlignRight
                                     : TextEdit.AlignHCenter
                verticalAlignment: !handle.liveStyle ? TextEdit.AlignVCenter
                                   : handle.liveStyle.valign === "top" ? TextEdit.AlignTop
                                   : handle.liveStyle.valign === "bottom" ? TextEdit.AlignBottom
                                   : TextEdit.AlignVCenter
                onTextChanged: {
                    if (!handle.editing)
                        return
                    EditorState.previewSetClipTextContent(
                        handle.box.track,
                        handle.box.clip,
                        text)
                }
                Keys.onEscapePressed: handle.cancelEdit()
                onActiveFocusChanged: if (!activeFocus && handle.editing) handle.commitEdit()
            }

            TapHandler {
                enabled: !handle.editing
                onTapped: (eventPoint) => {
                    // A tap inside a selected layer's frame picks the clip under the finger.
                    if (handle.isTransform) {
                        const p = root.mapFromItem(null, eventPoint.scenePosition.x,
                                                   eventPoint.scenePosition.y)
                        const hit = EditorState.previewClipAtCanvasPoint(p.x / handle.sx, p.y / handle.sy)
                        if (hit && hit.track !== undefined) {
                            EditorState.selectClip(hit.track, hit.clip)
                            Haptics.select()
                            return
                        }
                    }
                    EditorState.selectClip(handle.box.track, handle.box.clip)
                    handle.forceActiveFocus()
                    Haptics.select()
                }
            }

            // "Name · n clips" over a selected layer's frame.
            Rectangle {
                visible: handle.isTransform && handle.selected
                x: 0
                y: -height - Theme.spacingSm
                width: androidLayerLabel.implicitWidth + Theme.spacingLg
                height: androidLayerLabel.implicitHeight + Theme.spacingSm
                radius: Theme.radiusSm
                color: Theme.clipTransform

                Text {
                    id: androidLayerLabel
                    anchors.centerIn: parent
                    text: qsTr("%1 · %n clip(s)", "", handle.box.childCount || 0)
                              .arg(handle.box.name || qsTr("Transform"))
                    color: Theme.onMedia
                    font.family: Theme.fontFamily
                    font.pixelSize: Theme.fontSizeXs
                }
            }

            DragHandler {
                id: bodyDrag
                target: null
                // Only the clip selected on the timeline moves: dragging a box
                // that merely happens to be under the pointer used to shift the
                // wrong clip, so an unselected box is tap-to-select only.
                // Off while a grip is held too: a handler on the parent item can
                // otherwise take the grab from the grip once the drag threshold
                // is passed, turning a resize into a move.
                enabled: handle.selected && !handle.editing && !handle.resizing
                // 3D or parented: the press point in the box's own overlay frame, since
                // translation arrives through a projective matrix and is unreliable there.
                property point pressOverlay: Qt.point(0, 0)
                onActiveChanged: {
                    if (active) {
                        root.interacting = true
                        if (handle.isTransform)
                            root.movingLayerTrack = handle.box.track
                        pressOverlay = handle.toParentLocal(root.mapFromItem(null, centroid.scenePosition.x,
                                                                             centroid.scenePosition.y))
                        handle.dragStartX = handle.box.x
                        handle.dragStartY = handle.box.y
                        handle.liveX = handle.dragStartX
                        handle.liveY = handle.dragStartY
                        EditorState.selectClip(handle.box.track, handle.box.clip)
                        handle.forceActiveFocus()
                        EditorState.beginPreviewDrag()
                        Haptics.pickUp()
                    } else {
                        handle.liveX = -1e12
                        handle.liveY = -1e12
                        root.endInteraction()
                        Haptics.drop()
                    }
                }
                onTranslationChanged: {
                    let dx = 0
                    let dy = 0
                    if (handle.is3d || handle.hasParent) {
                        const p = handle.toParentLocal(root.mapFromItem(null, centroid.scenePosition.x,
                                                                        centroid.scenePosition.y))
                        dx = (p.x - pressOverlay.x) / handle.depthScale
                        dy = (p.y - pressOverlay.y) / handle.depthScale
                    } else {
                        // translation is in the rotated box frame; rotate it back to canvas axes
                        const a = handle.layoutRotation * Math.PI / 180
                        dx = translation.x * Math.cos(a) - translation.y * Math.sin(a)
                        dy = translation.x * Math.sin(a) + translation.y * Math.cos(a)
                    }
                    let xPx = handle.dragStartX + dx / handle.sx
                    let yPx = handle.dragStartY + dy / handle.sy
                    // Both edges and the centre stick, so a clip can be landed
                    // flush against a canvas edge or dead-centre by feel.
                    if (handle.canSnap) {
                        const w = handle.layoutW
                        const h = handle.layoutH
                        const snapX = root.snapAxis([xPx, xPx + w / 2, xPx + w],
                                                    handle.snapTargetsX, handle.snapTolX)
                        const snapY = root.snapAxis([yPx, yPx + h / 2, yPx + h],
                                                    handle.snapTargetsY, handle.snapTolY)
                        xPx += snapX.delta
                        yPx += snapY.delta
                        handle.publishGuides(snapX.guide, snapY.guide)
                    } else {
                        handle.publishGuides(-1, -1)
                    }
                    handle.liveX = xPx
                    handle.liveY = yPx
                    EditorState.previewSetClipPosition(
                        handle.box.track,
                        handle.box.clip,
                        xPx + handle.anchorOffsetX,
                        yPx + handle.anchorOffsetY)
                }
            }

            // The single move handle of a 3D model: an affordance at the box centre (the whole
            // box drags), where the corner and rotation grips would otherwise invite resizing.
            Rectangle {
                visible: handle.selected && handle.isModel3d && !handle.editing
                anchors.centerIn: parent
                width: 18
                height: 18
                radius: 9
                color: Theme.primary
                border.width: Theme.borderWidth
                border.color: Theme.onMedia
            }

            // Resize grips: 4 edges then 4 corners, the same frame the canvas crop
            // tool uses. `dx`/`dy` say which edges each grip moves (-1 = left/top,
            // +1 = right/bottom, 0 = stays put). The opposite edge or corner is the
            // anchor and does not move.
            Repeater {
                // A 3D layer is sized with the gizmo's scale tool instead.
                model: (handle.selected && !handle.editing && !handle.isModel3d && !handle.box.layer3d)
                       ? [
                           { dx: -1, dy:  0 },
                           { dx:  1, dy:  0 },
                           { dx:  0, dy: -1 },
                           { dx:  0, dy:  1 },
                           { dx: -1, dy: -1 },
                           { dx:  1, dy:  1 },
                           { dx:  1, dy: -1 },
                           { dx: -1, dy:  1 }
                       ]
                       : []

                delegate: Item {
                    id: grip
                    required property var modelData

                    // The touch target is the whole delegate; the dot inside it
                    // stays small enough not to hide the box edge it sits on.
                    width: root.gripTouch
                    height: root.gripTouch

                    // Corners drive both axes, edges only one — so an edge drag is
                    // the deliberate way to stretch and corners keep the ratio.
                    readonly property bool isCorner: modelData.dx !== 0 && modelData.dy !== 0
                    // Footage has a real aspect to protect. Boxes that exist to be
                    // reshaped (text, subtitles, shapes) scale freely.
                    readonly property bool lockRatio: isCorner
                            && (handle.box.kind === "video"
                                || handle.box.kind === "image"
                                || handle.box.kind === "composite")

                    x: (modelData.dx === 0 ? handle.width / 2
                                           : (modelData.dx < 0 ? 0 : handle.width)) - width / 2
                    y: (modelData.dy === 0 ? handle.height / 2
                                           : (modelData.dy < 0 ? 0 : handle.height)) - height / 2

                    Rectangle {
                        anchors.centerIn: parent
                        width: root.gripSize
                        height: root.gripSize
                        radius: Theme.radiusXs
                        color: gripArea.pressed ? Theme.primaryForeground : Theme.primary
                        border.width: Theme.borderWidth
                        border.color: gripArea.pressed ? Theme.primary : Theme.primaryForeground

                        Behavior on color {
                            ColorAnimation { duration: Theme.durationFast; easing.type: Theme.easing }
                        }
                    }

                    // Press point in overlay coordinates. The grip rides the box as
                    // it resizes, so deltas are measured against the overlay, which
                    // stands still.
                    property real startPx: 0
                    property real startPy: 0
                    // 3D only: the grab mapped into the box's own layout px.
                    property var toLocal: null
                    property point startLocal: Qt.point(0, 0)

                    // Resize about the fixed anchor. The maths runs in the box's own
                    // axes, so a rotated clip grows along the direction the grip
                    // points; the resulting centre shift is rotated back to canvas
                    // axes at the end. At rotation 0 it reduces to a plain "opposite
                    // corner stays put".
                    function resizeTo(px, py) {
                        const dxSign = grip.modelData.dx
                        const dySign = grip.modelData.dy
                        const a = handle.layoutRotation * Math.PI / 180
                        let lx = 0
                        let ly = 0
                        if (handle.is3d) {
                            // A tilted box resizes about its centre, which is also the tilt pivot:
                            // holding an edge still would drag it through depth. Doubling the
                            // finger's travel in the box plane keeps the grip under the finger.
                            const l = grip.toLocal(px, py)
                            lx = 2 * (l.x - grip.startLocal.x) / handle.sx
                            ly = 2 * (l.y - grip.startLocal.y) / handle.sy
                        } else {
                            const ddx = (px - grip.startPx) / handle.sx
                            const ddy = (py - grip.startPy) / handle.sy
                            // Canvas axes -> box axes: the inverse of the rotation the
                            // body drag applies to its translation.
                            lx = ddx * Math.cos(a) + ddy * Math.sin(a)
                            ly = -ddx * Math.sin(a) + ddy * Math.cos(a)
                        }

                        const locked = grip.lockRatio

                        let w = Math.max(1, handle.dragStartW + lx * dxSign)
                        let h = Math.max(1, handle.dragStartH + ly * dySign)
                        if (locked) {
                            // Take the scale from whichever axis the finger moved
                            // further along, measured as the bigger departure from
                            // the original size so it reads the same growing or
                            // shrinking.
                            const rw = w / handle.dragStartW
                            const rh = h / handle.dragStartH
                            const s = Math.abs(rw - 1) >= Math.abs(rh - 1) ? rw : rh
                            w = Math.max(1, handle.dragStartW * s)
                            h = Math.max(1, handle.dragStartH * s)
                        }

                        // Only the moving edge sticks; the anchor is already fixed.
                        let guideX = -1
                        let guideY = -1
                        if (handle.canSnap) {
                            const anchorX = dxSign < 0 ? handle.dragStartX + handle.dragStartW
                                                       : handle.dragStartX
                            const anchorY = dySign < 0 ? handle.dragStartY + handle.dragStartH
                                                       : handle.dragStartY
                            const snapX = dxSign === 0
                                    ? { delta: 0, guide: -1 }
                                    : root.snapAxis([anchorX + dxSign * w],
                                                    handle.snapTargetsX, handle.snapTolX)
                            const snapY = dySign === 0
                                    ? { delta: 0, guide: -1 }
                                    : root.snapAxis([anchorY + dySign * h],
                                                    handle.snapTargetsY, handle.snapTolY)
                            if (locked) {
                                // The axes are tied, so only the closer of the two
                                // snaps wins and it sets the scale for both.
                                const rx = snapX.guide >= 0 ? Math.abs(snapX.delta) : Infinity
                                const ry = snapY.guide >= 0 ? Math.abs(snapY.delta) : Infinity
                                let s = -1
                                if (rx <= ry && snapX.guide >= 0) {
                                    s = Math.abs(snapX.guide - anchorX) / handle.dragStartW
                                    guideX = snapX.guide
                                } else if (ry < Infinity) {
                                    s = Math.abs(snapY.guide - anchorY) / handle.dragStartH
                                    guideY = snapY.guide
                                }
                                if (s > 0) {
                                    w = Math.max(1, handle.dragStartW * s)
                                    h = Math.max(1, handle.dragStartH * s)
                                }
                            } else {
                                if (snapX.guide >= 0) {
                                    w = Math.max(1, w + snapX.delta * dxSign)
                                    guideX = snapX.guide
                                }
                                if (snapY.guide >= 0) {
                                    h = Math.max(1, h + snapY.delta * dySign)
                                    guideY = snapY.guide
                                }
                            }
                        }
                        handle.publishGuides(guideX, guideY)

                        // Keeping the anchor still means the centre moves by half the
                        // size change, toward the grip, in box axes.
                        const shiftX = handle.is3d ? 0 : (w - handle.dragStartW) / 2 * dxSign
                        const shiftY = handle.is3d ? 0 : (h - handle.dragStartH) / 2 * dySign
                        const cx = handle.dragStartX + handle.dragStartW / 2
                                   + shiftX * Math.cos(a) - shiftY * Math.sin(a)
                        const cy = handle.dragStartY + handle.dragStartH / 2
                                   + shiftX * Math.sin(a) + shiftY * Math.cos(a)
                        const x = cx - w / 2
                        const y = cy - h / 2

                        handle.liveX = x
                        handle.liveY = y
                        handle.liveW = w
                        handle.liveH = h
                        if (handle.isText && grip.isCorner) {
                            // Height drives the glyph scale; an edge drag just
                            // re-wraps, since the box is the wrap width.
                            const size = Math.round(handle.dragStartPixelSize
                                                    * h / Math.max(1, handle.dragStartH))
                            EditorState.previewSetTextRect(
                                handle.box.track,
                                handle.box.clip,
                                x, y, w, h, size)
                        } else {
                            EditorState.previewSetClipRect(
                                handle.box.track,
                                handle.box.clip,
                                x, y, w, h)
                        }
                    }

                    MouseArea {
                        id: gripArea
                        anchors.fill: parent
                        // The delegate's body DragHandler would otherwise take the
                        // grab once the drag threshold is passed, turning a resize
                        // into a move.
                        preventStealing: true

                        onPressed: (mouse) => {
                            const raw = mapToItem(root, mouse.x, mouse.y)
                            const p = handle.is3d ? raw : handle.toParentLocal(raw)
                            grip.startPx = p.x
                            grip.startPy = p.y
                            if (handle.is3d) {
                                grip.toLocal = handle.overlayToLocalMapper()
                                grip.startLocal = grip.toLocal(p.x, p.y)
                            }
                            if (handle.isTransform)
                                root.movingLayerTrack = handle.box.track
                            handle.dragStartX = handle.layoutX
                            handle.dragStartY = handle.layoutY
                            handle.dragStartW = handle.layoutW
                            handle.dragStartH = handle.layoutH
                            handle.dragStartPixelSize = handle.box.pixelSize || 64
                            handle.liveX = handle.dragStartX
                            handle.liveY = handle.dragStartY
                            handle.liveW = handle.dragStartW
                            handle.liveH = handle.dragStartH
                            handle.resizing = true
                            root.interacting = true
                            EditorState.selectClip(handle.box.track, handle.box.clip)
                            handle.forceActiveFocus()
                            EditorState.beginPreviewDrag()
                            Haptics.pickUp()
                        }

                        onPositionChanged: (mouse) => {
                            if (!pressed)
                                return
                            const raw = mapToItem(root, mouse.x, mouse.y)
                            const p = handle.is3d ? raw : handle.toParentLocal(raw)
                            grip.resizeTo(p.x, p.y)
                        }

                        onReleased: grip.finishResize()
                        onCanceled: grip.finishResize()
                    }

                    function finishResize() {
                        if (!handle.resizing)
                            return
                        handle.resizing = false
                        handle.liveX = -1e12
                        handle.liveY = -1e12
                        handle.liveW = -1
                        handle.liveH = -1
                        root.endInteraction()
                        Haptics.drop()
                    }
                }
            }

            // Live size while resizing, the same readout the crop tool shows.
            Rectangle {
                visible: handle.resizing
                anchors.horizontalCenter: parent.horizontalCenter
                anchors.top: parent.bottom
                anchors.topMargin: root.gripTouch / 2 + Theme.spacingSm
                width: sizeLabel.width + Theme.spacingLg
                height: sizeLabel.height + Theme.spacingSm
                radius: Theme.radiusSm
                // Sits on the preview, not a panel surface, so it uses the fixed
                // scrim and on-media foreground rather than panel tokens.
                color: Theme.scrimStrong
                border.width: Theme.borderWidth
                border.color: Theme.guideWeak
                // Keeps the number upright on a rotated clip.
                rotation: -handle.rotation

                Text {
                    id: sizeLabel
                    anchors.centerIn: parent
                    text: Math.round(handle.layoutW) + "×" + Math.round(handle.layoutH)
                    color: Theme.onMedia
                    font.family: Theme.monoFontFamily
                    font.pixelSize: Theme.fontSizeXs
                }
            }

            // Rotation handle above the box.
            Item {
                id: rotateGrip
                visible: handle.selected && !handle.editing && !handle.isModel3d && !handle.box.layer3d
                width: root.gripTouch
                height: root.gripTouch
                x: handle.width / 2 - width / 2
                y: -height - Theme.spacingLg

                Rectangle {
                    anchors.top: parent.verticalCenter
                    anchors.horizontalCenter: parent.horizontalCenter
                    width: 1
                    height: rotateGrip.height / 2 + Theme.spacingLg
                    color: Theme.primary
                }

                Rectangle {
                    anchors.centerIn: parent
                    width: root.gripSize + 6
                    height: width
                    radius: width / 2
                    color: rotateDrag.active ? Theme.primaryForeground : Theme.primary
                    border.width: 1
                    border.color: rotateDrag.active ? Theme.primary : Theme.onMedia
                }

                DragHandler {
                    id: rotateDrag
                    target: null
                    onActiveChanged: {
                        if (active) {
                            root.interacting = true
                            if (handle.isTransform)
                                root.movingLayerTrack = handle.box.track
                            handle.liveRotation = handle.box.rotation
                            EditorState.selectClip(handle.box.track, handle.box.clip)
                            handle.forceActiveFocus()
                            EditorState.beginPreviewDrag()
                            Haptics.pickUp()
                        } else {
                            handle.liveRotation = 1e9
                            root.endInteraction()
                            Haptics.drop()
                        }
                    }
                    onCentroidChanged: {
                        if (!active)
                            return
                        const p = handle.toParentLocal(root.mapFromItem(null, rotateDrag.centroid.scenePosition.x,
                                                                        rotateDrag.centroid.scenePosition.y))
                        // A tilted box spins about its projected centre.
                        const c = handle.is3d
                                ? handle.toParentLocal(handle.mapToItem(root, handle.layoutW * handle.sx / 2,
                                                                        handle.layoutH * handle.sy / 2))
                                : Qt.point(handle.centerX, handle.centerY)
                        const ang = Math.atan2(p.y - c.y, p.x - c.x)
                        const deg = ang * 180 / Math.PI + 90
                        handle.liveRotation = deg
                        EditorState.previewSetClipRotation(
                            handle.box.track,
                            handle.box.clip,
                            deg)
                    }
                }
            }
        }
    }

    // With a clip inside a transform layer selected: the layer's frame, dashed, and a
    // finger-sized chip that selects it.
    Item {
        id: parentFrame
        anchors.fill: parent
        z: 880
        visible: root.parentFrameBox !== null && !root.interacting
        readonly property var quad: root.parentFrameBox ? (root.parentFrameBox.quad || []) : []
        readonly property real qsx: root.width / Math.max(1, root.parentFrameBox ? root.parentFrameBox.canvasWidth : 1)
        readonly property real qsy: root.height / Math.max(1, root.parentFrameBox ? root.parentFrameBox.canvasHeight : 1)
        function corner(i) {
            const q = parentFrame.quad[i]
            return q ? Qt.point(q.x * qsx, q.y * qsy) : Qt.point(0, 0)
        }

        Shape {
            anchors.fill: parent
            visible: parentFrame.quad.length === 4
            preferredRendererType: Shape.CurveRenderer
            ShapePath {
                strokeColor: Theme.clipTransform
                strokeWidth: Theme.borderWidthFocus
                strokeStyle: ShapePath.DashLine
                dashPattern: [4, 3]
                fillColor: "transparent"
                startX: parentFrame.corner(0).x
                startY: parentFrame.corner(0).y
                PathLine { x: parentFrame.corner(1).x; y: parentFrame.corner(1).y }
                PathLine { x: parentFrame.corner(2).x; y: parentFrame.corner(2).y }
                PathLine { x: parentFrame.corner(3).x; y: parentFrame.corner(3).y }
                PathLine { x: parentFrame.corner(0).x; y: parentFrame.corner(0).y }
            }
        }

        Rectangle {
            visible: parentFrame.quad.length === 4
            x: Math.max(0, Math.min(root.width - width, parentFrame.corner(0).x))
            y: Math.max(0, parentFrame.corner(0).y - height - Theme.spacingSm)
            width: androidParentChipLabel.implicitWidth + Theme.spacingXl * 2
            height: 48
            radius: height / 2
            color: parentChipTap.pressed ? Qt.lighter(Theme.clipTransform, 1.15) : Theme.clipTransform

            Text {
                id: androidParentChipLabel
                anchors.centerIn: parent
                text: qsTr("Select %1").arg(root.parentFrameBox ? (root.parentFrameBox.name || qsTr("Transform")) : "")
                color: Theme.onMedia
                font.family: Theme.fontFamily
                font.pixelSize: Theme.fontSizeSm
            }

            TapHandler {
                id: parentChipTap
                onTapped: {
                    Haptics.select()
                    EditorState.selectTransformParent()
                }
            }
        }
    }

    TransformGizmo {
        id: gizmo
        anchors.fill: parent
        z: 950
        touch: true
        sx: root.width / Math.max(1, root.gizmoBox ? root.gizmoBox.canvasWidth : 1)
        // Hidden while the box itself is dragged, which it would not follow.
        box: root.interacting && dragging === "" ? null : root.gizmoBox
        onDragStarted: {
            root.interacting = true
            EditorState.selectClip(box.track, box.clip)
            Haptics.pickUp()
        }
        onDragFinished: {
            root.endInteraction()
            Haptics.drop()
        }
    }

    // Alignment guides for whichever snap is currently engaged. Drawn once at
    // root level so a guide spans the whole canvas, not just the clip box.
    Rectangle {
        visible: root.snapGuideX >= 0
        x: root.snapGuideX
        y: 0
        width: 1
        height: root.height
        color: Theme.primary
        z: 400
    }
    Rectangle {
        visible: root.snapGuideY >= 0
        x: 0
        y: root.snapGuideY
        width: root.width
        height: 1
        color: Theme.primary
        z: 400
    }

    // Tap-away catcher: while a text clip is edited in place, a press outside the
    // (raised) editor box drops focus, which commits the edit via the editor's
    // onActiveFocusChanged.
    MouseArea {
        anchors.fill: parent
        z: 500
        visible: root.editingKey !== ""
        enabled: visible
        onPressed: (mouse) => {
            root.forceActiveFocus()
            mouse.accepted = true
        }
    }
}
